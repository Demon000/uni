plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'com.google.protobuf' version '0.8.8'
}

version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def log4jVersion = '2.13.1'
def grpcVersion = '1.29.0'
def protobufVersion = '3.11.0'
def rabbitMqVersion = '5.9.0'

dependencies {
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: "${log4jVersion}"
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: "${log4jVersion}"

    compile group: 'com.rabbitmq', name: 'amqp-client', version: "${rabbitMqVersion}"

    compile group: 'com.google.protobuf', name: 'protobuf-java', version: "${protobufVersion}"
    compile group: 'io.grpc', name: 'grpc-all', version: "${grpcVersion}"
    if (JavaVersion.current().isJava9Compatible()) {
        // Workaround for @javax.annotation.Generated
        // see: https://github.com/grpc/grpc-java/issues/3633
        compile 'javax.annotation:javax.annotation-api:1.3.1'
    }

    protobuf files('../common/proto')
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

javafx {
    modules = [ 'javafx.controls', 'javafx.fxml' ]
    version = '11.0.2'
}

application {
    mainClassName = "Main"
}
